<?xml version="1.0" encoding="utf-8"?>
<xsl:stylesheet 
  version="1.0" 
  xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
  xmlns:msxsl="urn:schemas-microsoft-com:xslt" 
  exclude-result-prefixes="msxsl"
  >
  
  <xsl:output method="text"/>

  <xsl:template name="getModuleTypeName">
    <xsl:choose>
      <xsl:when test="@type='P'">Procedure</xsl:when>
      <xsl:when test="@type='V'">View</xsl:when>
      <xsl:when test="@type='FN' or @type='FS' or @type='FT' or @type='IF'">Function</xsl:when>
      <xsl:otherwise>'<xsl:value-of select="@type"/>'</xsl:otherwise>
    </xsl:choose>  
  </xsl:template>

  <xsl:template name="getModuleFullName">
    <xsl:call-template name="getModuleTypeName"/> - <xsl:value-of select="concat('[', @schema, ']', '.', '[', @name, ']')" />      
  </xsl:template>
  
  <xsl:template name="outputModule">
-------------------------------------------------------------------------
--
-- <xsl:call-template name="getModuleFullName"/>
--
-------------------------------------------------------------------------
    <xsl:value-of select="text()" />

  </xsl:template>
  
  <xsl:template match="/">
---------------------------------------------------------
-------------- GENERATED BY getsqlcode.xslt -------------
---------------------------------------------------------

------------------------ SUMMARY ------------------------
    <xsl:for-each select="//module">
      <xsl:sort select="@schema" />
      <xsl:sort select="@name" />
-- <xsl:call-template name="getModuleFullName"/>
    </xsl:for-each>

--------------------- END OF SUMMARY --------------------


    <xsl:for-each select="//module">
      <xsl:sort select="@schema" />
      <xsl:sort select="@name" />
      <xsl:call-template name="outputModule" />
    </xsl:for-each>
  </xsl:template>
</xsl:stylesheet>
